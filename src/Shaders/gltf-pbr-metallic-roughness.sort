@Pass{
#extension GL_OES_standard_derivatives : enable
@BlendFunc(SRC_ALPHA,ONE_MINUS_SRC_ALPHA)
@ExposeMacro(int,jointCount,JOINT_COUNT,0)
FS_PREC(mediump,float)

varying vec3 vPosition;
varying vec3 vNormal;
varying vec2 vUV;

#ifdef VS
#if JOINT_COUNT > 0
  @JOINTMATRIX
  uniform mat4 boneMatrices[JOINT_COUNT];
#endif
  @NORMAL
  attribute vec3 normal;
  @POSITION
  attribute vec3 position;
#ifdef ATTRIBUTE_TEXCOORD_0_ENABLED
  @TEXCOORD_0
  attribute vec2 texCoord;
#endif
#if JOINT_COUNT > 0
  @JOINTS_0
  attribute vec4 joint;
  @WEIGHTS_0
  attribute vec4 weight;
#endif
  uniform mat4 _matPVM;
  uniform mat4 _matM;
  void main(){
    mat4 transform = _matM;
    mat4 projectionTransform = _matPVM;
    #if JOINT_COUNT > 0
      mat4 skinMat = weight.x * boneMatrices[int(joint.x)] + weight.y * boneMatrices[int(joint.y)] + weight.z * boneMatrices[int(joint.z)] + weight.w * boneMatrices[int(joint.w)];
      transform *= skinMat;
      projectionTransform *= skinMat;
    #endif
    #ifdef ATTRIBUTE_TEXCOORD_0_ENABLED
        vUV = texCoord;
    #else
        vUV  = position.xy /2.0 + vec2(0.5);
    #endif
    vNormal = normalize((transform * vec4(normal,0.0)).xyz);
    vPosition = (transform * vec4(position,1.0)).xyz;
    gl_Position = projectionTransform * vec4(position,1.0);
  }


#endif


#ifdef FS
  @{flag:"USE_NORMAL_TEXTURE"}
  uniform sampler2D normalTexture;

  @{default:"white",type:"color"}
  uniform vec4 baseColorFactor;

  @{flag:"USE_BASECOLOR_TEXTURE"}
  uniform sampler2D baseColorTexture;

  @{default:"1"}
  uniform float metallicFactor;

  @{flag:"USE_METALIC_TEXTURE"}
  uniform sampler2D metallicTexture;

  @{default:"1"}
  uniform float roughnessFactor;

  @{flag:"USE_ROUGHNESS_TEXTURE"}
  uniform sampler2D roughnessTexture;

  @{default:"black",type:"color"}
  uniform vec3 emissiveFactor;

  @{flag:"USE_EMISSIVE_TEXTURE"}
  uniform sampler2D emissiveTexture;

  @{flag:"USE_METALIC_ROUGHNESS_TEXTURE"}
  uniform sampler2D metallicRoughnessTexture;

  @{flag:"USE_OCCLUSION_TEXTURE"}
  uniform sampler2D occlusionTexture;

  @CAMERA_POSITION
  uniform vec3 _cameraPosition;

  @import "forward-shading"

  #ifndef GR_FORWARD_SHADING_ENABLED

  struct simple_pbr_params{
    vec3 diffuseColor;
    vec3 f0;
    float alpha;
    float roughness;
  };

    @{default:"n(3,0.5,10)"}
    uniform vec3 simpleLightDirection;

    @{type:"color",default:"gray"}
    uniform vec3 simpleLightColor;

    @{default:0.3}
    uniform float simpleLightIntencity;

    float lambert(vec3 lightDirection,vec3 surfaceNormal) {
      return max(0.0, dot(lightDirection, surfaceNormal));
    }

    float ctd_GGX_Distribution(simple_pbr_params param,vec3 l,vec3 v,vec3 n,vec3 h){
      float alpha2 = pow(param.alpha,2.0);
      float nh2 = pow(dot(n,h),2.0);
      return alpha2/(PI*pow(nh2*(alpha2 - 1.0) + 1.0,2.0));
    }

    float ctg_GGX_SingleGeometryTerm(simple_pbr_params param,vec3 n,vec3 v){
      float d = dot(n,v);
      return 2.*d /(d + sqrt(d*d + param.alpha*param.alpha*(1. - d*d)));
    }

    vec3 ctf_Schlick(simple_pbr_params param,vec3 l,vec3 v,vec3 n,vec3 h){
      vec3 f0 = param.f0;
      float vh = dot(v,n);
      return f0 + pow(1.0-vh,5.0) * (vec3(1.0) - f0);
    }

    vec3 cookTorranceBRDF(simple_pbr_params param,vec3 l,vec3 v,vec3 n){
      vec3 h = normalize(l+v);
      return  ctf_Schlick(param,l,v,n,h) * ctd_GGX_Distribution(param,l,v,n,h) * ctg_GGX_SingleGeometryTerm(param,n,l) * ctg_GGX_SingleGeometryTerm(param,n,v)/(4.0 * dot(l,n) * dot(v,n));
    }

    vec3 BRDF(simple_pbr_params params,vec3 li,vec3 lo,vec3 n){
      return params.diffuseColor/PI + cookTorranceBRDF(params,li,lo,n);
    }

  #endif


  void main(){
    vec4 baseColor = baseColorFactor;
    #ifdef USE_BASECOLOR_TEXTURE
      baseColor *= texture2D(baseColorTexture,vUV);
    #endif
    vec3 emissive = emissiveFactor;
    #ifdef USE_EMISSIVE_TEXTURE
      emissive *= texture2D(emissiveTexture,vUV).rgb;
    #endif
    float metallic = metallicFactor;
    #ifdef USE_METALIC_TEXTURE
      metallic *= texture2D(metallicTexture,vUV).r;
    #endif
    float roughness = roughnessFactor;
    #ifdef USE_ROUGHNESS_TEXTURE
      roughness *= texture2D(roughnessTexture,vUV).r;
    #endif
    #ifdef USE_METALIC_ROUGHNESS_TEXTURE
      vec3 rm = texture2D(metallicRoughnessTexture,vUV).rgb;
      metallic *= rm.b;
      roughness *= rm.g;
    #endif
    vec3 normal = vNormal;
    #ifdef USE_NORMAL_TEXTURE
      vec3 pos_dx = dFdx(vPosition);
      vec3 pos_dy = dFdy(vPosition);
      vec3 tex_dx = dFdx(vec3(vUV, 0.0));
      vec3 tex_dy = dFdy(vec3(vUV, 0.0));
      vec3 t = (tex_dy.t * pos_dx - tex_dx.t * pos_dy) / (tex_dx.s * tex_dy.t - tex_dy.s * tex_dx.t);
      t = normalize(t - vNormal * dot(vNormal, t));
      vec3 b = normalize(cross(vNormal, t));
      mat3 tbn = mat3(t, b, vNormal);
      vec3 n = texture2D(normalTexture, vUV).rgb;
      normal = normalize(tbn * (2.0 * n - 1.0));
    #endif
    vec3 dielectricSpecular = vec3(0.04);
    vec3 diffuse = mix(baseColor.rgb * (1. - dielectricSpecular.r),vec3(0),metallic);
    vec3 f0 = mix(dielectricSpecular,baseColor.rgb,metallic);
    float alpha = roughness * roughness;
    #ifdef GR_FORWARD_SHADING_ENABLED
      pbr_params param = pbr_params(diffuse,f0,alpha,roughness);
      gl_FragColor.rgb = shading(param,normal,vPosition);
    #endif
    #ifndef GR_FORWARD_SHADING_ENABLED
      simple_pbr_params params = simple_pbr_params(diffuse,f0,alpha,roughness);
      float lam = lambert(simpleLightDirection,normal);
      vec3 brdfCoeff = BRDF(params,-simpleLightDirection,normalize(_cameraPosition - vPosition),normal);
      gl_FragColor.rgb = vec3(lam) * simpleLightIntencity * simpleLightColor * brdfCoeff;
    #endif
    gl_FragColor.rgb += emissive;
    #ifdef USE_OCCLUSION_TEXTURE
      gl_FragColor.rgb *= texture2D(occlusionTexture,vUV).r;
    #endif
    gl_FragColor.a = baseColor.a;
  }
#endif
}
